group 'com.aquivalabs.force.ant'
version '0.9'

buildscript {
    ext.kotlin_version = '1.0.4'
    ext.ant_version = '1.9.6'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://dl.bintray.com/kotlinx/kotlinx"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx.html.jvm:0.5.10'
    compile "org.apache.ant:ant:$ant_version"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'org.testng:testng:6.9.10'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'net.sf.saxon:Saxon-HE:9.7.0-7'
    testCompile 'org.xmlmatchers:xml-matchers:1.0-RC1'
    testCompile "com.nhaarman:mockito-kotlin:0.6.0"
    testCompile 'org.jsoup:jsoup:1.9.2'
}

test {
    useTestNG()
}

jar {
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': version
        attributes 'Ant-Version': "Apache Ant $ant_version"
        attributes 'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')'
    }
    from('src/main/kotlin') {
        include '**/antlib.xml'
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "org/apache/tools/ant/"
        exclude "images/ant_logo_large.gif"
        exclude "kotlinx.html.shared/"
        exclude "kotlinx.html.shared.*.js"
    }
    with jar
}

artifacts {
    archives jar
    archives fatJar
}